PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

PREFIX bfo: <http://purl.obolibrary.org/obo/BFO_>
PREFIX ro: <http://purl.obolibrary.org/obo/RO_>
PREFIX cco: <http://www.ontologyrepository.com/CommonCoreOntologies/>

SELECT DISTINCT ?targetTerm ?label ?termType ?targetSuperClass ?targetSuperProperty WHERE {
  # What kinds of things are we mapping?
  VALUES ?termType { owl:Class owl:ObjectProperty }

  # Where are we mapping from? Exclude these
  FILTER( ! ( STRSTARTS(STR(?targetTerm), STR(bfo:)) || STRSTARTS(STR(?targetTerm), STR(ro:)) || STRSTARTS(STR(?targetTerm), STR(cco:)) ) ) .
  
  # How are we mapping (which predicates) ?
  # Level 1: every target term must be subsumed by some term OR all of its subterms are subsumed by some source term
  # Level 2: every target term must be subsumed by some term
  # Level 3: every target term must be subsumed by some source term
  # Level 4: every target term must be equivalent to some source term
  FILTER NOT EXISTS {
    VALUES ?mappingPredicate { rdfs:subClassOf rdfs:subPropertyOf owl:equivalentClass owl:equivalentProperty owl:disjointUnionOf } .
    FILTER(?sourceTerm != owl:topObjectProperty) .
    ?targetTerm ?mappingPredicate ?sourceTerm .
    # # Union in order to include equivalence axioms
    # { ?targetTerm ?mappingPredicate ?sourceTerm . } UNION { ?sourceTerm ?mappingPredicate ?targetTerm . }
  }

  ?targetTerm a ?termType .
  ?targetTerm rdfs:label ?label .
  OPTIONAL { ?targetTerm rdfs:subClassOf ?targetSuperClass } .
  OPTIONAL { ?targetTerm rdfs:subPropertyOf ?targetSuperProperty } .
}
ORDER BY ?term