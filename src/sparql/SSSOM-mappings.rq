PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX bfo: <http://purl.obolibrary.org/obo/BFO_>
PREFIX cco: <https://www.commoncoreontologies.org/>
PREFIX ro: <http://purl.obolibrary.org/obo/RO_>

# NOTE: This does not include complex mappings. We await consensus from SSSOM on how to represent these.
# See: https://github.com/mapping-commons/SSSOM/issues/36
# See: https://mapping-commons.github.io/sssom/spec/#common-mapping-predicates

SELECT DISTINCT ?subject_id	?subject_label ?predicate_id ?object_id ?object_label ?mapping_justification WHERE {
    ?subject_id ?predicate_id ?object_id .

    # mapping predicates
    # TODO `robot relax` to get subClassOf axioms from equivalentClass
    FILTER( ?predicate_id = rdfs:subClassOf 
            || ?predicate_id = rdfs:subPropertyOf 
            || ?predicate_id = owl:equivalentClass
            || ?predicate_id = owl:equivalentProperty) .

    # mapping source
    FILTER( STRSTARTS( STR(?subject_id), str(prov:) ) ) .

    # mapping destination
    FILTER( STRSTARTS( STR(?object_id), str(bfo:) ) || STRSTARTS( STR(?object_id), str(cco:) ) || STRSTARTS( STR(?object_id), str(ro:) ) ) .

    # mapping justification 
    VALUES (?mapping_justification) {
        ("https://w3id.org/semapv/vocab/ManualMappingCuration")
    }

    OPTIONAL { ?subject_id rdfs:label ?subject_label } .
    OPTIONAL { ?object_id rdfs:label ?object_label } . 
}
ORDER BY ?subject_id ?predicate_id ?object_id